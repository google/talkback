// For building the open-source release of TalkBack.

apply plugin: 'com.android.application'

apply from: 'shared.gradle'

apply from: 'version.gradle'

final BUILD_TIMESTAMP = new Date().format('yyyy_MM_dd_HHmm')

dependencies {
    implementation project(':talkback')
}

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.4.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

android {
    namespace talkbackApplicationId
    defaultConfig {
        applicationId talkbackApplicationId
        versionName talkbackVersionName + "-" + BUILD_TIMESTAMP
        minSdk 26
        targetSdk 30
        compileSdk 34
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'README'
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    productFlavors {
        phone {
            dimension "target"
        }
        wear {
            dimension "target"
            versionNameSuffix "-wear"
        }
    }
}
