// For building the open-source release of TalkBack.

ext {
    talkbackApplicationId = "com.android.talkback4d"
}

android {
    compileSdkVersion 34
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        minSdkVersion 26
    }
    flavorDimensions "target"
    productFlavors {
        phone {
            dimension "target"
        }
        wear {
            dimension "target"
        }
    }
}

configurations.all {
    resolutionStrategy {
        eachDependency {
            // https://paste.googleplex.com/6584345232932864?raw
            //
            // 'androidx.wear:wear:1.2.0-rc01'
            // 'androidx.wear:wear-remote-interactions:1.0.0'
            //
            // If the above actual dependencies are updated and these libraries
            // update the version of org.jetbrains.kotlin:kotlin-stdlib, we
            // should update useVersion("1.8.0") to the updated version. Or, we
            // can remove these lines once these libraries don't conflict with
            // each other anymore.
            if ((requested.group == "org.jetbrains.kotlin") && (requested.name.startsWith("kotlin-stdlib"))) {
                useVersion("1.8.0")
            }
        }
    }
}

dependencies {

    // Google common
    implementation 'com.google.guava:guava:31.0-jre'
    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:17.0.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.google.android.play:core:1.10.3'

    // Support library
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.collection:collection:1.1.0'
    implementation 'androidx.core:core:1.10.0-beta01'
    implementation 'androidx.fragment:fragment:1.4.0-alpha08'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // Nullable
    implementation 'org.checkerframework:checker-qual:2.8.1'

    // Auto-value
    implementation 'com.google.auto.value:auto-value-annotations:1.8.2'
    annotationProcessor 'com.google.auto.value:auto-value:1.8.2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'androidx.wear:wear:1.2.0-rc01'
    implementation 'androidx.wear:wear-remote-interactions:1.0.0'
    implementation 'com.google.android.support:wearable:2.8.1'

    implementation 'com.google.zxing:core:3.5.1'

    // UI understanding
    implementation 'joda-time:joda-time:2.9.9'
}

android {
    defaultConfig {
        buildConfigField("String", "APPLICATION_ID", "\"" + talkbackApplicationId + "\"")
    }
}
